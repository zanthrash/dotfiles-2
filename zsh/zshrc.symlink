ZSH=$HOME/.oh-my-zsh
DOTFILES=$HOME/.dotfiles

# oh-mh-zsh theme
ZSH_THEME="agnoster"

# oh-my-zsh aliases
alias zshconfig="st ~/.zshrc"
alias ohmyzsh="st ~/.oh-my-zsh"

# disable update, upgrade_dotfiles handles this
DISABLE_AUTO_UPDATE="true"

upgrade_dotfiles() {
  if [[ `pwd` == $HOME/.dotfiles ]] ; then
    ./update.sh
  else
    pushd ~/.dotfiles > /dev/null
    ./bootstrap.sh update
    popd > /dev/null
  fi
}

# for the over eager upgrade junky
yippee-ki-yay() {
    upgrade_dotfiles
}

# define oh-my-zsh plugins implicitly based on the topics
plugins=("${(@f)$(find $DOTFILES -not -name '.git' -d 1 -type d -exec basename {} \;)}")

plugins+=("${(@f)$(find $DOTFILES/local -not -name '.git' -d 1 -type d -exec basename {} \;)}")

# add any plugins defined by files
plugins+=("${(@f)$(find -L $DOTFILES -name oh-my-zsh.plugins -d 2 -exec cat {} \;)}")

# load oh-my-zsh
source $ZSH/oh-my-zsh.sh

# use .localrc for things that need to be kept secret
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# make sure DEFAULT_USER has been set
if [ -z "$DEFAULT_USER" ]; then
  echo "Please set DEFAULT_USER in your ~/.localrc"
  exit 1
fi

# all of our zsh files
typeset -U config_files
config_files=($(find -L "$DOTFILES" -name \*.zsh))

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# put the bin/ directories first on the path
export PATH=$DOTFILES/bin:$DOTFILES/local/bin:$PATH

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files
